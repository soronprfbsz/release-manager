plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'  // Protobuf 플러그인
}

group = 'com.ts'
version = '0.0.1-SNAPSHOT'
description = 'Release Manager'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

// UTF-8 인코딩 설정
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

tasks.withType(Test) {
	systemProperty 'file.encoding', 'UTF-8'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

	// SSH & SFTP
	implementation 'com.github.mwiede:jsch:0.2.16'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

	// Flyway
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// MapStruct
	implementation 'org.mapstruct:mapstruct:1.6.3'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

	// QueryDSL
	implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

	// gRPC
	implementation 'io.grpc:grpc-netty-shaded:1.76.0'
	implementation 'io.grpc:grpc-protobuf:1.76.0'
	implementation 'io.grpc:grpc-stub:1.76.0'

	// Annotation API
	implementation 'javax.annotation:javax.annotation-api:1.3.2' // @PostConstruct, @PreDestroy 지원

	// .env 파일 지원
	implementation 'me.paulschwarz:spring-dotenv:4.0.0'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.12.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Spring Boot 3.x plain JAR 생성 비활성화 (아티팩트 크기 감소)
tasks.named('jar') {
	enabled = false
}

// QueryDSL 설정
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

sourceSets {
	main.java.srcDirs += querydslDir
}

tasks.withType(JavaCompile).configureEach {
	options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

clean {
	delete file(querydslDir)
}

// Protobuf 설정
protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.25.3'
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.76.0'
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		proto {
			// proto 파일 위치 지정
			srcDir 'src/main/java/com/springboot_boilerplate/global/grpc/proto'
		}
		java {
			// 생성된 Java 파일 경로 추가
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

// compileJava가 generateProto에 의존하도록 설정
tasks.named('compileJava') {
	dependsOn 'generateProto'
}
